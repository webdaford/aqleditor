use dataverse tpch;

declare type LineItemType as closed {
  l_orderkey: int32, 
  l_linenumber: int32
}

declare type OrderType as closed {
  o_orderkey: int32
}

declare type CustomerType as closed {
  c_custkey: int32
}

declare type SupplierType as closed {
  s_suppkey: int32
}

declare type NationType as closed {
  n_nationkey: int32
}

declare type RegionType as closed {
  r_regionkey: int32
}

declare type PartType as closed {
  p_partkey: int32
}

declare type PartSuppType as closed {
  ps_partkey: int32, 
  ps_suppkey: int32 
}

declare nodegroup group1 on nc1, nc2;

set format "edu.uci.ics.asterix.runtime.formats.nontagged.NonTaggedDataFormat";

declare dataset LineItems(LineItemType)
  primary key l_orderkey, l_linenumber on group1;
declare dataset Orders(OrderType)
  primary key o_orderkey on group1;
declare dataset Suppliers(SupplierType)
  primary key s_suppkey on group1;
declare dataset Nations(NationType) 
  primary key n_nationkey on group1;
declare dataset Parts(PartType)
  primary key p_partkey on group1;
declare dataset PartSupp(PartSuppType)
  primary key ps_partkey, ps_suppkey on group1;  
  
write output to nc1:"/home/yasser/Dropbox/Research/data/results/result_tpch_closed_9.adm";

                
for $profit in 
(  for $o in dataset('Orders')
   for $l3 in 
   (  for $p in dataset('Parts')
      for $l2 in 
      (  for $ps in dataset('PartSupp')
         for $l1 in 
        (  for $s1 in
            (  for $s in dataset('Suppliers')
               for $n in dataset('Nations')
               where $n.n_nationkey = $s.s_nationkey
               return 
                 { "s_suppkey": $s.s_suppkey, "n_name": $n.n_name}  )
            for $l in dataset('LineItems')
            where $s1.s_suppkey = $l.l_suppkey       
            return 
              { "l_suppkey": $l.l_suppkey, "l_extendedprice": $l.l_extendedprice, "l_discount": $l.l_discount,
                "l_quantity": $l.l_quantity, "l_partkey": $l.l_partkey, "l_orderkey": $l.l_orderkey, "n_name": $s1.n_name } )
         where $ps.ps_suppkey = $l1.l_suppkey and $ps.ps_partkey = $l1.l_partkey       
         return 
           { "l_extendedprice": $l1.l_extendedprice, "l_discount": $l1.l_discount, "l_quantity": $l1.l_quantity, 
             "l_partkey": $l1.l_partkey, "l_orderkey": $l1.l_orderkey, "n_name": $l1.n_name, "ps_supplycost": $ps.ps_supplycost }  )
      where contains($p.p_name, 'green') and $p.p_partkey = $l2.l_partkey    
      return 
        { "l_extendedprice": $l2.l_extendedprice, "l_discount": $l2.l_discount, "l_quantity": $l2.l_quantity, 
          "l_orderkey": $l2.l_orderkey, "n_name": $l2.n_name, "ps_supplycost": $l2.ps_supplycost  }  )
   where $o.o_orderkey = $l3.l_orderkey
   let $amount := $l3.l_extendedprice * (1 - $l3.l_discount) -  $l3.ps_supplycost * $l3.l_quantity 
   let $o_year := year($o.o_orderdate)
   return 
     { "nation": $l3.n_name, "o_year": $o_year, "amount": $amount }  )
 group by $nation := $profit.nation, $o_year := $profit.o_year with $profit
 order by $nation, $o_year desc
 return 
   { "nation": $nation, 
     "o_year": $o_year, 
      "sum_profit": sum( for $pr in $profit return $pr.amount )  }    
   